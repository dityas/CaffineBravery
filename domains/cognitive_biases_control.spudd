(defvar HostType    (c nc hp))
(defvar AccessLevel (user root))

-- Defender modeling the attacker
(defvar Defl1ModelsAttl0        (m0))
(defvar Defl1ModelsAttl0EC      (ec0))
(defvar PassiveDefl1ModelsAttl0         (m0))
(defvar PassiveDefl1ModelsAttl0EC       (ec0))

(defvar Attl2ModelsDefl1        (m0 m1 m2 m3 m4 m5))
(defvar Attl2ModelsDefl1EC      (ec0))

(defvar AttackerFrame   (att))
(defvar DefFrame        (highCap lowCap))

-- Actions for both agents
(defvar DefActions (BLOCK_ACTION NOP))
(defvar AttActions (RECON PRIV_ESC EXFIL NOP EXIT))

-- Defender observations
(defvar HType (obs_c obs_nc obs_hp))

-- Attacker observations
(defvar ReconObs (protected_asset unprotected_asset blocked none))

-- Initial belief for defender
(defdd initDef
    (HostType hp)
)

-- Initial belief for attacker
(defdd initAtt
    (HostType 
        (c      (0.333333))
        (nc     (0.333333))
        (hp     (0.333334))
    )
)

-- L0 Attacker's guess of L1 defender's action distribution
(defdd distDefActions
    (DefActions
        (BLOCK_ACTION   (0.01))
        (NOP            (0.99))
    )
)

-- Attacker's Priv Esc

(defdd privEscAccessLevel
    (DefActions
        (BLOCK_ACTION
            (SAME AccessLevel)
        )

        (NOP
            (AccessLevel' root)
        )
    )
)

(defdd privEscReconObs
    (AccessLevel'
        (user   (ReconObs' blocked))
        (root   (ReconObs' none))
    )
)

-- Attacker's RECON action

-- When defender does not block the attacker's actions
(defdd reconObsDefNop
    (HostType'
        (c      
            (ReconObs' 
                (protected_asset    (0.9))
                (unprotected_asset  (0.04))
                (blocked            (0.02))
                (none               (0.04))
            )
        )

        (nc     
            (ReconObs' 
                (protected_asset    (0.04))
                (unprotected_asset  (0.04))
                (blocked            (0.02))
                (none               (0.9))
            )
        )

        (hp     
            (ReconObs' 
                (protected_asset    (0.04))
                (unprotected_asset  (0.9))
                (blocked            (0.02))
                (none               (0.04))
            )
        )
    )
)

-- When defender blocks the attacker's actions
(defdd reconObsDefBlockAction
    (HostType'
        (c      
            (ReconObs' 
                (protected_asset    (0.08))
                (unprotected_asset  (0.01))
                (blocked            (0.9))
                (none               (0.01))
            )
        )

        (nc     
            (ReconObs' 
                (protected_asset    (0.01))
                (unprotected_asset  (0.01))
                (blocked            (0.9))
                (none               (0.08))
            )
        )

        (hp     
            (ReconObs' 
                (protected_asset    (0.01))
                (unprotected_asset  (0.08))
                (blocked            (0.9))
                (none               (0.01))
            )
        )
    )
)

-- Combined observ function based on joint actions
(defdd reconObs
    (DefActions
        (BLOCK_ACTION   (reconObsDefBlockAction))
        (NOP    (reconObsDefNop))
    )
)

-- recon observation for other actions
(defdd reconObsDefault
    (ReconObs'
       (protected_asset    (0.04))
       (unprotected_asset  (0.04))
       (blocked            (0.02))
       (none               (0.9))
    )
)

(defdd reconObsExfil
    (DefActions
        (BLOCK_ACTION
            (ReconObs'                            
               (protected_asset    (0.04))
               (unprotected_asset  (0.04))
               (blocked            (0.9))
               (none               (0.02)) 
            )                             
        )

        (NOP
            (ReconObs'                    
               (protected_asset    (0.04))
               (unprotected_asset  (0.04))
               (blocked            (0.02))
               (none               (0.9)) 
            )                             
        )
    )
)

(defdbn attackerReconDBN
    (HostType       (SAME HostType))
    (AccessLevel    (SAME AccessLevel))
    (ReconObs       (# (DefActions) (distDefActions * reconObs)))
)

(defdbn attackerExfilDBN
    (HostType   (SAME HostType))
    (AccessLevel    (SAME AccessLevel))
    (ReconObs   (# (DefActions) (distDefActions * reconObsExfil)))
)

(defdbn attackerExitDBN
    (HostType   (SAME HostType))
    (AccessLevel    (SAME AccessLevel))
    (ReconObs   (reconObsDefault))
)

(defdbn attackerNopDBN
    (HostType   (SAME HostType)
    (AccessLevel    (SAME AccessLevel))
    (ReconObs   (reconObsDefault))
)

(defdbn attackerPrivEscDBN
    (HostType   (SAME HostType)
    (AccessLevel    (# (DefActions) (distDefActions * privEscAccessLevel)))
    (ReconObs   (privEscReconObs))
)
-- multi agent reward DD for level 0

(defdd exfilReward

    (DefActions
        (NOP
            (AccessLevel
                (user   (-1.0))
                (root
                    (HostType
                        (c      (10.0))
                        (nc     (-20.0))
                        (hp     (-20.0))
                    )
                )
            )
        )
        (BLOCK_ACTION   (-1.0))
    )
)

-- Attacker model
(defpomdp attl0
    (S
        (HostType AccessLevel)
    )

    (O
        (ReconObs)
    )

    (A  AttActions)

    (dynamics
        (RECON  (attackerReconDBN))
        (EXFIL  (attackerExfilDBN))
        (NOP    (attackerNopDBN))
        (EXIT   (attackerExitDBN))
        (PRIV_ESC   (attackerPrivEscDBN))
    )
    
    (R
        (RECON      (-1.0))
        (PRIV_ESC   (-1.0))
        (NOP    (-0.5))
        (EXFIL  
            (# (DefActions) (distDefActions * exfilReward)) 
        )

        (EXIT
            (HostType
                (c      (-1.0))
                (nc     (0.0))
                (hp     (0.0))
            )
        )
    )

    (discount 0.9)
)

-- Initial belief of the attacker
(defdd initAttl0
    (HostType
        (c      (0.33333))
        (nc     (0.33333))
        (hp     (0.33334))
    )

    * (AccessLevel user)
)

(initmodelvar Defl1ModelsAttl0
    (frames AttackerFrame)
    (
        (att    (m0     (initAttl0)))
    )
)

-- Defender's DDs
(defdd defObs
    (HostType'
        (c
            (HType'
                (obs_c  (0.99))
                (obs_nc (0.005))
                (obs_hp (0.005))
            )
        )

        (nc
            (HType'
                (obs_c  (0.005))
                (obs_nc (0.99))
                (obs_hp (0.005))
            )

        )

        (hp
            (HType'
                (obs_c  (0.005))
                (obs_nc (0.005))
                (obs_hp (0.99))
            )

        )
    )
)

(defdd attActHostType
    (AttActions
        (RECON  (SAME HostType))
        (EXFIL  (SAME HostType))
        (NOP    (SAME HostType))
        (EXIT   (SAME HostType))
        (PRIV_ESC   (SAME HostType))
    )
)

(defdd attActAccessLevel
    (AttActions
        (RECON  (SAME AccessLevel))
        (EXFIL  (SAME AccessLevel))
        (NOP    (SAME AccessLevel))
        (EXIT   (SAME AccessLevel))
        (PRIV_ESC   (AccessLevel' root))
    )
)

(defdbn actionBlockAction
    (HostType       (attActHostType))
    (AccessLevel    (attActAccessLevel))
    (HType          (defObs))
)

(defdbn actionNOP
    (HostType   (attActHostType))
    (AccessLevel    (attActAccessLevel))
    (HType      (defObs))
)

-- defender L1 model
(defipomdp defl1
    (S
        (HostType AccessLevel)
    )

    (O
        (HType)
    )

    (A  DefActions)
    (Aj AttActions)

    (Mj Defl1ModelsAttl0)
    (EC Defl1ModelsAttl0EC)

    (Thetaj AttackerFrame
        (att    attl0)
    )

    (dynamics
        (BLOCK_ACTION   (actionBlockAction))
        (NOP            (actionNOP))
    )
    
    (R
        (BLOCK_ACTION   (AttActions
            (RECON      (-1.0))
            (EXFIL      (10.0))
            (PRIV_ESC   (10.0))
            (EXIT       (-100.0))
            (NOP        (-100.0))
        ))
        (NOP            (-0.1))
    )

    (discount 0.9)
    (H 6)
)

(defdd initDefl1Actual
    (HostType
        (c      (0.333333))
        (nc     (0.333333))
        (hp     (0.333334))
    )
    * (AccessLevel user)
    * (Defl1ModelsAttl0 m0)
)

-- Initial beliefs of l1 defender

(defdd initDefl1c
    (HostType   c)
    * (AccessLevel user)
    * (Defl1ModelsAttl0 m0)
)

(defdd initDefl1nc
    (HostType   nc)
    * (AccessLevel user)
    * (Defl1ModelsAttl0 m0)
)

(defdd initDefl1hp
    (HostType   hp)
    * (AccessLevel user)
    * (Defl1ModelsAttl0 m0)
)

-- Make passive defender model

(initmodelvar PassiveDefl1ModelsAttl0
    (frames AttackerFrame)
    (
        (att    (m0     (initAttl0)))
    )
)

-- passive defender l1 model
(defipomdp defl1Passive
    (S
        (HostType AccessLevel)
    )

    (O
        (HType)
    )

    (A  DefActions)
    (Aj AttActions)

    (Mj PassiveDefl1ModelsAttl0)
    (EC PassiveDefl1ModelsAttl0EC)

    (Thetaj AttackerFrame
        (att    attl0)
    )

    (dynamics
        (BLOCK_ACTION   (actionBlockAction))
        (NOP            (actionNOP))
    )
    
    (R
        (BLOCK_ACTION   -(1.0))
        (NOP            (0.0))
    )

    (discount 0.9)
    (H 6)
)

(defdd initPassiveDefl1c
    (HostType   c)
    * (AccessLevel user)
    * (PassiveDefl1ModelsAttl0 m0)
)

(defdd initPassiveDefl1nc
    (HostType   nc)
    * (AccessLevel user)
    * (PassiveDefl1ModelsAttl0 m0)
)

(defdd initPassiveDefl1hp
    (HostType   hp)
    * (AccessLevel user)
    * (PassiveDefl1ModelsAttl0 m0)
)

-- Attacker l2

(defdbn attackerReconlNDBN
    (HostType       (SAME HostType))
    (AccessLevel    (SAME AccessLevel))
    (ReconObs       (reconObs))
)

(defdbn attackerExfillNDBN
    (HostType   (SAME HostType))
    (AccessLevel    (SAME AccessLevel))
    (ReconObs   (reconObsExfil))
)

(defdbn attackerExitlNDBN
    (HostType   (SAME HostType))
    (AccessLevel    (SAME AccessLevel))
    (ReconObs   (reconObsDefault))
)

(defdbn attackerNoplNDBN
    (HostType   (SAME HostType)
    (AccessLevel    (SAME AccessLevel))
    (ReconObs   (reconObsDefault))
)

(defdbn attackerPrivEsclNDBN
    (HostType       (SAME HostType)
    (AccessLevel    (privEscAccessLevel))
    (ReconObs       (privEscReconObs))
)

(initmodelvar Attl2ModelsDefl1
    (frames DefFrame)
    (
        (lowCap    (m0     (initPassiveDefl1c)))
        (lowCap    (m1     (initPassiveDefl1nc)))
        (lowCap    (m2     (initPassiveDefl1hp)))
        (highCap   (m3     (initDefl1c)))
        (highCap   (m4     (initDefl1nc)))
        (highCap   (m5     (initDefl1hp)))
    )
)

-- Level 2 Attacker model
(defipomdp attl2
    (S
        (HostType AccessLevel)
    )

    (O
        (ReconObs)
    )

    (A  AttActions)
    (Aj DefActions)

    (Mj Attl2ModelsDefl1)
    (EC Attl2ModelsDefl1EC)

    (Thetaj DefFrame
        (lowCap    defl1Passive)
        (highCap   defl1)
    )

    (dynamics
        (RECON  (attackerReconlNDBN))
        (EXFIL  (attackerExfillNDBN))
        (NOP    (attackerNoplNDBN))
        (EXIT   (attackerExitlNDBN))
        (PRIV_ESC   (attackerPrivEsclNDBN))
    )

    (R
        (RECON      (-1.0))
        (PRIV_ESC   (-1.0))
        (NOP    (-0.5))
        (EXFIL  
            (exfilReward) 
        )

        (EXIT
            (HostType
                (c      (-1.0))
                (nc     (0.0))
                (hp     (0.0))
            )
        )
    )

    (discount 0.9)
    (H 7)
)

(defdd dependentMj
    (HostType
        (c
            (Attl2ModelsDefl1
                (m0     (0.5))
                (m1     (0.0))
                (m2     (0.0))
                (m3     (0.5))
                (m4     (0.0))
                (m5     (0.0))
            )      
        )

        (nc
            (Attl2ModelsDefl1
                (m0     (0.0))
                (m1     (0.5))
                (m2     (0.0))
                (m3     (0.0))
                (m4     (0.5))
                (m5     (0.0))
            )
        )

        (hp
            (Attl2ModelsDefl1
                (m0     (0.0))
                (m1     (0.0))
                (m2     (0.5))
                (m3     (0.0))
                (m4     (0.0))
                (m5     (0.5))
            )
        )
    )
)

(defdd initAttl2
    (HostType
        (c      (0.33))
        (nc     (0.33))
        (hp     (0.34))
    )
    * (AccessLevel user)
    * (dependentMj)
)
