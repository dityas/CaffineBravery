(defvar HostType        (critical honeypot critical_honeypot))
(defvar HostHasData     (yes no))
(defvar EscAccounts     (yes no))
(defvar AccessLevel     (user root))
(defvar AttActive       (yes no))
(defvar DataInjected    (yes no))

-- Defender modeling the attacker
(defvar Defl1ModelsAttl0            (m0))
(defvar Defl1ModelsAttl0EC          (ec0))
(defvar PassiveDefl1ModelsAttl0     (m0))
(defvar PassiveDefl1ModelsAttl0EC   (ec0))

(defvar Attl2ModelsDefl1    (m0 m1 m2 m3))
(defvar Attl2ModelsDefl1EC  (ec0))

(defvar Defl3ModelsAttl2    (m0))
(defvar Defl3ModelsAttl2EC  (ec0))

(defvar AttackerFrame   (att))
(defvar DefFrame        (highCap lowCap))

-- Actions for both agents
(defvar DefActions      
    (LockAccounts RestrictAccPrivs Nop DeployData DecoyAccount))
(defvar AttActions      
    (FileRecon EnumAccounts PrivEscAcc Nop Exit ManipulateData))

-- Defender observations
(defvar EndPtAlerts  
    (file_enum file_write process_creation login_activity none))

-- Attacker observations
(defvar AccessPerm      (auth denied))
(defvar HostObs         (user_files none))
(defvar ActionResult    (success failure))
(defvar ConnStatus      (active terminated))

-- L0 Attacker's guess of L1 defender's action distribution
(defdd distDefActions
    (DefActions
        (LockAccounts       (0.01))
        (DeployData         (0.01))
        (DecoyAccount       (0.01))
        (RestrictAccPrivs   (0.01))
        (Nop                (0.96))
    )
)

--------------------------------------------------------------------

(defdd privEscAccDD
    (AttActive
        (yes
            (EscAccounts
                (yes    (AccessLevel' root))
                (no     (SAME AccessLevel))
            )
        )
        (no     (SAME AccessLevel))
    )
)

(defdd connStatusObs
    (AttActive'
        (yes    (ConnStatus' active))
        (no     (ConnStatus' terminated))
    )
)

(defdd hostObsDD
    (HostType'
        (critical
            (HostObs'
                (user_files     (0.99))
                (none           (0.01))
            )
        )

        (honeypot
            (HostObs'
                (user_files     (0.01))
                (none           (0.99))
            )
        )

        (critical_honeypot
            (HostObs'   UNIFORM)
        )
    )
)

----------------------------------------------------------
-- Attacker's RECON action
-- When defender does not block the attacker's actions

(defdd escAccountObs
    (AttActive'
        (yes
            (EscAccounts'
                (yes
                    (ActionResult'
                        (success    (0.8))
                        (failure    (0.2))
                    )
                )
                (no
                    (ActionResult'
                        (success    (0.2))
                        (failure    (0.8))
                    )
                )
            )
        )
        (no     (ActionResult' UNIFORM))
    )
)

(defdd fileReconObs
    (AttActive'
        (yes    
            (HostHasData'
                (yes
                    (ActionResult'
                        (success    (0.8))
                        (failure    (0.2))
                    )
                )

                (no
                    (ActionResult'
                        (success    (0.2))
                        (failure    (0.8))
                    )
                )
            )
        )

        (no     (ActionResult' UNIFORM))
    )
)

(defdd defActionsEscAccounts
    (DefActions
        (LockAccounts       (EscAccounts' no))
        (RestrictAccPrivs   (SAME EscAccounts))
        (DeployData         (SAME EscAccounts))
        (DecoyAccount       (EscAccounts' yes))
        (Nop                (SAME EscAccounts))
    )
)

(defdd defActionsHostHasData
    (DefActions
        (LockAccounts       (SAME HostHasData))
        (RestrictAccPrivs   (SAME HostHasData))
        (DeployData         (HostHasData' yes))
        (DecoyAccount       (SAME HostHasData))
        (Nop                (SAME HostHasData))
    )
)

(defdd accessPermsObs
    (AccessLevel'
        (user           (AccessPerm'    denied))
        (root           (AccessPerm'    auth))
    )
)

(defdd manipulateDataDD
    (HostHasData
        (yes
            (AccessLevel
                (user       (SAME DataInjected))
                (root       (DataInjected' yes))
            )
        )

        (no     (SAME DataInjected))
    )
)

(defdbn attackerFileReconDBN
    (HostType       (SAME HostType))
    (HostHasData    (# (DefActions) 
        (distDefActions * defActionsHostHasData)))
    (EscAccounts    (# (DefActions) 
        (distDefActions * defActionsEscAccounts)))
    (AccessLevel    (SAME AccessLevel))
    (AttActive      (SAME AttActive)
    (DataInjected   (SAME DataInjected))

    (HostObs        (hostObsDD))
    (AccessPerm     (accessPermsObs))
    (ActionResult   (fileReconObs))
    (ConnStatus     (connStatusObs))
)

(defdbn attackerEnumAccountsDBN
    (HostType       (SAME HostType))
    (HostHasData    (# (DefActions) 
        (distDefActions * defActionsHostHasData)))
    (EscAccounts    (# (DefActions) 
        (distDefActions * defActionsEscAccounts)))
    (AccessLevel    (SAME AccessLevel))
    (AttActive      (SAME AttActive)
    (DataInjected   (SAME DataInjected))

    (HostObs        (HostObs' UNIFORM))
    (AccessPerm     (AccessPerm'    UNIFORM))
    (ActionResult   (escAccountObs))
    (ConnStatus     (connStatusObs))
)

(defdbn attackerExitDBN
    (HostType       (SAME HostType))
    (HostHasData    (# (DefActions) 
        (distDefActions * defActionsHostHasData)))
    (EscAccounts    (# (DefActions) 
        (distDefActions * defActionsEscAccounts)))
    (AccessLevel    (SAME AccessLevel))
    (AttActive      (AttActive' no))
    (DataInjected   (SAME DataInjected))

    (HostObs        (HostObs' UNIFORM))
    (AccessPerm     (AccessPerm'    UNIFORM))
    (ActionResult   (ActionResult'  UNIFORM))
    (ConnStatus     (connStatusObs))
)

(defdbn attackerNopDBN
    (HostType       (SAME HostType))
    (HostHasData    (# (DefActions) 
        (distDefActions * defActionsHostHasData)))
    (EscAccounts    (# (DefActions) 
        (distDefActions * defActionsEscAccounts)))
    (AccessLevel    (SAME AccessLevel))
    (AttActive      (SAME AttActive))
    (DataInjected   (SAME DataInjected))

    (HostObs        (HostObs' UNIFORM))
    (AccessPerm     (AccessPerm'    UNIFORM))
    (ActionResult   (ActionResult'  UNIFORM))
    (ConnStatus     (connStatusObs))
)

(defdbn attackerPrivEscAccDBN
    (HostType       (SAME HostType))
    (HostHasData    (# (DefActions) 
        (distDefActions * defActionsHostHasData)))
    (EscAccounts    (# (DefActions) 
        (distDefActions * defActionsEscAccounts)))
    (AccessLevel    (privEscAccDD))
    (AttActive      (SAME AttActive))
    (DataInjected   (SAME DataInjected))

    (HostObs        (HostObs' UNIFORM))
    (AccessPerm     (accessPermsObs))
    (ActionResult   (ActionResult'  UNIFORM))
    (ConnStatus     (connStatusObs))
)

(defdbn attackerManipulateDataDBN
    (HostType       (SAME HostType))
    (HostHasData    (SAME HostHasData))
    (EscAccounts    (# (DefActions) 
        (distDefActions * defActionsEscAccounts)))
    (AccessLevel    (SAME AccessLevel))
    (AttActive      (SAME AttActive))
    (DataInjected   (manipulateDataDD))

    (HostObs        (HostObs' UNIFORM))
    (AccessPerm     (accessPermsObs))
    (ActionResult   (ActionResult'  UNIFORM))
    (ConnStatus     (connStatusObs))
)

-- multi agent reward DD for level 0

(defdd exitReward
    (AttActive
        (yes
            (HostType
                (critical
                    (HostHasData
                        (yes
                            (DataInjected
                                (yes    (30.0))
                                (no     (-5.0))
                            )
                        )

                        (no     (1.0))
                    )
                )

                (honeypot   (1.0))

                (critical_honeypot
                    (HostHasData
                        (yes
                            (DataInjected
                                (yes    (30.0))
                                (no     (-5.0))
                            )
                        )

                        (no     (1.0))
                    )
                )
            )
        )

        (no     (-1.0))
    )
)

(defdd privEscAccReward
    (AttActive
        (yes
            (EscAccounts
                (yes    (-1.0))
                (no     (-10.0))
            )
        )
        (no     (-1.0))
    )
)

(defdd nopCost
    (AttActive
        (yes    -(1.0))
        (no     (0.0))
    )
)

-- Attacker model
(defpomdp attl0
    (S
        (HostType HostHasData EscAccounts AccessLevel AttActive DataInjected)
    )

    (O
        (AccessPerm ActionResult ConnStatus HostObs)
    )

    (A  AttActions)

    (dynamics
        (FileRecon          (attackerFileReconDBN))
        (Nop                (attackerNopDBN))
        (Exit               (attackerExitDBN))
        (ManipulateData     (attackerManipulateDataDBN))
        (EnumAccounts       (attackerEnumAccountsDBN))
        (PrivEscAcc         (attackerPrivEscAccDBN))
    )
    
    (R
        (FileRecon          (-1.0))
        (EnumAccounts       (-1.0))
        (PrivEscAcc         (privEscAccReward))
        (ManipulateData     (-2.0))
        (Nop                (nopCost))
        (Exit               (exitReward))
    )

    (discount 0.95)
)

-- Initial belief of the attacker
(defdd initAttl0
    (HostType   
        (critical           (0.5))
        (honeypot           (0.5))
        (critical_honeypot  (0.0))
    )
    * (HostHasData      UNIFORM)
    * (EscAccounts      UNIFORM)
    * (AccessLevel      user)
    * (AttActive        yes)
    * (DataInjected     no)
)

---------------------------------------------------------------
-- Defender l1 models

(initmodelvar Defl1ModelsAttl0
    (frames AttackerFrame)
    (
        (att    (m0     (initAttl0)))
    )
)
---------------------------------------------------------------
---- Observations
(defdd defaultEndPtAlerts
    (EndPtAlerts'
        (file_enum              (0.05))
        (file_write             (0.05))
        (login_activity         (0.05))
        (none                   (0.85))
    )
)

(defdd hostReconEndPtAlerts
    (EndPtAlerts'
        (file_enum              (0.85))
        (file_write             (0.05))
        (login_activity         (0.05))
        (none                   (0.05))
    )
)

(defdd manipulateDataEndPtAlerts
    (EndPtAlerts'
        (file_enum              (0.05))
        (file_write             (0.85))
        (login_activity         (0.05))
        (none                   (0.05))
    )
)

(defdd privEscEndPtAlert
    (EndPtAlerts'
        (file_enum              (0.05))
        (file_write             (0.05))
        (login_activity         (0.85))
        (none                   (0.05))
    )
)

(defdd endPtAlertsObs
    (AttActive'
        (yes
            (AttActions
                (FileRecon          (hostReconEndPtAlerts))
                (EnumAccounts       (hostReconEndPtAlerts))
                (ManipulateData     (manipulateDataEndPtAlerts))
                (PrivEscAcc         (privEscEndPtAlert))
                (Nop                (defaultEndPtAlerts))
                (Exit               (defaultEndPtAlerts))
            )
        )

        (no     (defaultEndPtAlerts))
    )
)

(defdd attActionsAccessLevel
    (AttActions
        (FileRecon          (SAME AccessLevel))
        (EnumAccounts       (SAME AccessLevel))
        (ManipulateData     (SAME AccessLevel))
        (PrivEscAcc         (privEscAccDD))
        (Nop                (SAME AccessLevel))
        (Exit               (SAME AccessLevel))
    )
)

(defdd attActionsAttActive
    (AttActions
        (FileRecon          (SAME AttActive))
        (EnumAccounts       (SAME AttActive))
        (ManipulateData     (SAME AttActive))
        (PrivEscAcc         (SAME AttActive))
        (Nop                (SAME AttActive))
        (Exit               (AttActive' no))
    )
)

(defdd attActionsDataInjected
    (AttActions
        (FileRecon          (SAME DataInjected))
        (EnumAccounts       (SAME DataInjected))
        (ManipulateData     (manipulateDataDD))
        (PrivEscAcc         (SAME DataInjected))
        (Nop                (SAME DataInjected))
        (Exit               (SAME DataInjected))
    )
)

(defdbn actionLockAccounts
    (HostType       (SAME HostType))
    (HostHasData    (SAME HostHasData))
    (EscAccounts    (EscAccounts' no))
    (AccessLevel    (attActionsAccessLevel))
    (AttActive      (attActionsAttActive))
    (DataInjected   (attActionsDataInjected))

    (EndPtAlerts    (endPtAlertsObs))
)

(defdbn actionNop
    (HostType       (SAME HostType))
    (HostHasData    (SAME HostHasData))
    (EscAccounts    (SAME EscAccounts))
    (AccessLevel    (attActionsAccessLevel))
    (AttActive      (attActionsAttActive))
    (DataInjected   (attActionsDataInjected))

    (EndPtAlerts    (endPtAlertsObs))
)

(defdbn actionRestrictAccPrivs
    (HostType       (SAME HostType))
    (HostHasData    (SAME HostHasData))
    (EscAccounts    (SAME EscAccounts))
    (AccessLevel    (attActionsAccessLevel))
    (AttActive      (attActionsAttActive))
    (DataInjected   (attActionsDataInjected))

    (EndPtAlerts    (endPtAlertsObs))
)

(defdbn actionDeployData
    (HostType       (SAME HostType))
    (HostHasData    (HostHasData' yes))
    (EscAccounts    (SAME EscAccounts))
    (AccessLevel    (attActionsAccessLevel))
    (AttActive      (attActionsAttActive))
    (DataInjected   (attActionsDataInjected))

    (EndPtAlerts    (endPtAlertsObs))
)

(defdbn actionDecoyAccount
    (HostType       (SAME HostType))
    (HostHasData    (SAME HostHasData))
    (EscAccounts    (EscAccounts' yes))
    (AccessLevel    (attActionsAccessLevel))
    (AttActive      (attActionsAttActive))
    (DataInjected   (attActionsDataInjected))

    (EndPtAlerts    (endPtAlertsObs))
)

(defdd stateReward
    (HostType
        (critical
            (DataInjected
                (yes    (-10.0))
                (no     (1.0))
            )
        )
        (honeypot   
            (AttActive
                (yes    (5.0))
                (no     (0.0))
            )
        )
        (critical_honeypot  (0.0))
    )
)

-- defender L1 model
(defipomdp defl1_active
    (S
        (HostType HostHasData EscAccounts AccessLevel AttActive DataInjected)
    )

    (O
        (EndPtAlerts)
    )

    (A  DefActions)
    (Aj AttActions)

    (Mj Defl1ModelsAttl0)
    (EC Defl1ModelsAttl0EC)

    (Thetaj AttackerFrame
        (att    attl0)
    )

    (dynamics
        (LockAccounts       (actionLockAccounts))
        (RestrictAccPrivs   (actionRestrictAccPrivs))
        (Nop                (actionNop))
        (DeployData         (actionDeployData))
        (DecoyAccount       (actionDecoyAccount))
    )
    
    (R
        (LockAccounts       (stateReward - 1.0))
        (RestrictAccPrivs   (stateReward - 1.0))
        (Nop                (stateReward - 0.5))
        (DeployData         (stateReward - 1.0))
        (DecoyAccount       (stateReward - 1.0))
    )

    (discount 0.95)
    (H 7)
)

(defdd initDefl1_active
    (HostType           critical)
    * (EscAccounts      yes)
    * (HostHasData      yes)
    * (AccessLevel      user)
    * (AttActive        yes)
    * (DataInjected     no)
    * (Defl1ModelsAttl0 m0)
)

------ Initial beliefs of l1 defender

(defdd initDefl1c_active
    (HostType           critical)
    * (EscAccounts      yes)
    * (HostHasData      yes)
    * (AccessLevel      user)
    * (AttActive        yes)
    * (DataInjected     no)
    * (Defl1ModelsAttl0 m0)
)

(defdd initDefl1hp_active
    (HostType           honeypot)
    * (EscAccounts      no)
    * (HostHasData      no)
    * (AccessLevel      user)
    * (AttActive        yes)
    * (DataInjected     no)
    * (Defl1ModelsAttl0 m0)
)


-- Make passive defender model

(initmodelvar PassiveDefl1ModelsAttl0
    (frames AttackerFrame)
    (
        (att    (m0     (initAttl0)))
    )
)

-- passive defender l1 model
(defipomdp defl1_passive
    (S
        (HostType HostHasData EscAccounts AccessLevel AttActive DataInjected)
    )

    (O
        (EndPtAlerts)
    )

    (A  DefActions)
    (Aj AttActions)

    (Mj PassiveDefl1ModelsAttl0)
    (EC PassiveDefl1ModelsAttl0EC)

    (Thetaj AttackerFrame
        (att    attl0)
    )

    (dynamics
        (LockAccounts       (actionLockAccounts))
        (RestrictAccPrivs   (actionRestrictAccPrivs))
        (Nop                (actionNop))
        (DeployData         (actionDeployData))
        (DecoyAccount       (actionDecoyAccount))
    )
    
    (R
        (LockAccounts       (stateReward - 10.0))
        (RestrictAccPrivs   (stateReward - 1.0))
        (Nop                (stateReward - 0.5))
        (DeployData         (stateReward - 10.0))
        (DecoyAccount       (stateReward - 10.0))
    )

    (discount 0.95)
    (H 7)
)


(defdd initDefl1_passive
    (HostType           critical)
    * (EscAccounts      yes)
    * (HostHasData      yes)
    * (AccessLevel      user)
    * (AttActive        yes)
    * (DataInjected     no)
    * (PassiveDefl1ModelsAttl0 m0)
)

(defdd initDefl1c_passive
    (HostType           critical)
    * (EscAccounts      yes)
    * (HostHasData      yes)
    * (AccessLevel      user)
    * (AttActive        yes)
    * (DataInjected     no)
    * (PassiveDefl1ModelsAttl0 m0)
)

(defdd initDefl1hp_passive
    (HostType           honeypot)
    * (EscAccounts      no)
    * (HostHasData      no)
    * (AccessLevel      user)
    * (AttActive        yes)
    * (DataInjected     no)
    * (PassiveDefl1ModelsAttl0 m0)
)

---- Attacker l2

(defdbn actionAttFileReconDBN
    (HostType       (SAME HostType))
    (HostHasData    (defActionsHostHasData))
    (EscAccounts    (defActionsEscAccounts))
    (AccessLevel    (SAME AccessLevel))
    (AttActive      (SAME AttActive))
    (DataInjected   (SAME DataInjected))

    (HostObs        (hostObsDD))
    (AccessPerm     (accessPermsObs))
    (ActionResult   (fileReconObs))
    (ConnStatus     (connStatusObs))
)

(defdbn actionAttEnumAccountsDBN
    (HostType       (SAME HostType))
    (HostHasData    (defActionsHostHasData))
    (EscAccounts    (defActionsEscAccounts))
    (AccessLevel    (SAME AccessLevel))
    (AttActive      (SAME AttActive))
    (DataInjected   (SAME DataInjected))

    (HostObs        (HostObs' UNIFORM))
    (AccessPerm     (AccessPerm'    UNIFORM))
    (ActionResult   (escAccountObs))
    (ConnStatus     (connStatusObs))
)

(defdbn actionAttExitDBN
    (HostType       (SAME HostType))
    (HostHasData    (defActionsHostHasData))
    (EscAccounts    (defActionsEscAccounts))
    (AccessLevel    (SAME AccessLevel))
    (AttActive      (AttActive' no))
    (DataInjected   (SAME DataInjected))

    (HostObs        (HostObs' UNIFORM))
    (AccessPerm     (AccessPerm'    UNIFORM))
    (ActionResult   (ActionResult'  UNIFORM))
    (ConnStatus     (connStatusObs))
)

(defdbn actionAttNopDBN
    (HostType       (SAME HostType))
    (HostHasData    (defActionsHostHasData))
    (EscAccounts    (defActionsEscAccounts))
    (AccessLevel    (SAME AccessLevel))
    (AttActive      (SAME AttActive))
    (DataInjected   (SAME DataInjected))

    (HostObs        (HostObs' UNIFORM))
    (AccessPerm     (AccessPerm'    UNIFORM))
    (ActionResult   (ActionResult'  UNIFORM))
    (ConnStatus     (connStatusObs))
)

(defdbn actionAttPrivEscAccDBN
    (HostType       (SAME HostType))
    (HostHasData    (defActionsHostHasData))
    (EscAccounts    (defActionsEscAccounts))
    (AccessLevel    (privEscAccDD))
    (AttActive      (SAME AttActive))
    (DataInjected   (SAME DataInjected))

    (HostObs        (HostObs' UNIFORM))
    (AccessPerm     (accessPermsObs))
    (ActionResult   (ActionResult'  UNIFORM))
    (ConnStatus     (connStatusObs))
)

(defdbn actionAttManipulateDataDBN
    (HostType       (SAME HostType))
    (HostHasData    (defActionsHostHasData))
    (EscAccounts    (defActionsEscAccounts))
    (AccessLevel    (SAME AccessLevel))
    (AttActive      (SAME AttActive))
    (DataInjected   (manipulateDataDD))

    (HostObs        (HostObs' UNIFORM))
    (AccessPerm     (accessPermsObs))
    (ActionResult   (ActionResult'  UNIFORM))
    (ConnStatus     (connStatusObs))
)

(initmodelvar Attl2ModelsDefl1
    (frames DefFrame)
    (
        (lowCap     (m0 (initDefl1c_passive)))
        (lowCap     (m1 (initDefl1hp_passive)))
        (highCap    (m2 (initDefl1c_active)))
        (highCap    (m3 (initDefl1hp_active)))
    )
)

---- Level 2 Attacker model
(defipomdp attl2
    (S
        (HostType EscAccounts HostHasData AccessLevel AttActive DataInjected)
    )

    (O
        (AccessPerm ActionResult ConnStatus HostObs)
    )

    (A  AttActions)
    (Aj DefActions)

    (Mj Attl2ModelsDefl1)
    (EC Attl2ModelsDefl1EC)

    (Thetaj DefFrame
        (lowCap    defl1_passive)
        (highCap   defl1_active)
    )

    (dynamics
        (FileRecon          (actionAttFileReconDBN))
        (Nop                (actionAttNopDBN))
        (Exit               (actionAttExitDBN))
        (ManipulateData     (actionAttManipulateDataDBN))
        (EnumAccounts       (actionAttEnumAccountsDBN))
        (PrivEscAcc         (actionAttPrivEscAccDBN))
    )

    (R
        (FileRecon          (-1.0))
        (EnumAccounts       (-1.0))
        (PrivEscAcc         (privEscAccReward))
        (ManipulateData     (-2.0))
        (Nop                (nopCost))
        (Exit               (exitReward))
    )

    (discount 0.95)
    (H 10)
)

(defdd dependentMjAttl2
    (HostType
        (critical
            (Attl2ModelsDefl1
                (m0     (0.5))
                (m1     (0.0))
                (m2     (0.5))
                (m3     (0.0))
            )      
        )

        (honeypot
            (Attl2ModelsDefl1
                (m0     (0.0))
                (m1     (0.5))
                (m2     (0.0))
                (m3     (0.5))
            )
        )

        (critical_honeypot
            (Attl2ModelsDefl1
                (m0     (0.25))
                (m1     (0.25))
                (m2     (0.25))
                (m3     (0.25))
        )
    )
)

(defdd initAttl2
    (HostType   
        (critical           (0.5))
        (honeypot           (0.5))
        (critical_honeypot  (0.0))
    )
    * (HostHasData      UNIFORM)
    * (EscAccounts      UNIFORM)
    * (AccessLevel      user)
    * (AttActive        yes)
    * (DataInjected     no)
    * (dependentMjAttl2)
)

(def initState
    (HostType           honeypot)
    * (HostHasData      yes)
    * (EscAccounts      yes)
    * (AccessLevel      user)
    * (AttActive        yes)
    * (DataInjected     no)
)
